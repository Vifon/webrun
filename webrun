#!/usr/bin/env perl

use warnings;
use strict;

use LWP::UserAgent;

use File::Temp qw(tempfile);
use File::Basename;


# saves $data from $url in a temporary file
# and returns its name
sub save_to_temp_file {
    my ($data, $url) = @_;

    # try to guess the downloaded file's extension
    my $suffix = "";
    if (basename($url) =~ /.*(\..*)/) {
        $suffix = $1;
        $suffix =~ s/\?.*//;
    }

    my ($fd, $name) = tempfile(SUFFIX => $suffix,
                               UNLINK => 1);
    print $fd $data;
    close $fd;
    return $name;
}

my $ua = LWP::UserAgent->new;
$ua->agent("webrun/1.0 ");
for (@ARGV) {
    # if a given argument is an url
    if (m,^https?://,) {
        my $req = HTTP::Request->new(GET => $_);
        my $res = $ua->request($req);
        # skip failed downloads
        if (not $res->is_success) {
            # replace the url in ARGV with /dev/null
            $_ = "/dev/null";
        } else {
            # replace the url in ARGV with a temporary file
            $_ = save_to_temp_file($res->content, $_);
        }
    }
}

# Using exec would prevent the tempfile removal.
exit system @ARGV;
