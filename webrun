#!/usr/bin/env perl

use warnings;
use strict;

use WWW::Curl::Easy;
sub downloader {
    my $curl = WWW::Curl::Easy->new;
    # store a given $url's contents in the $output reference
    sub {
        my ($url, $output) = @_;

        $curl->setopt(CURLOPT_URL       , $url);
        $curl->setopt(CURLOPT_WRITEDATA , $output);
        $curl->perform;
    }
}


use File::Temp qw(tempfile);
use File::Basename;


# saves $data from $url in a temp file
# and returns its name
sub save_to_temp_file {
    my ($data, $url) = @_;

    basename($url) =~ /.*(\..*)/;
    my $suffix = $1;
    $suffix =~ s/\?.*//;

    my ($file, $name) = tempfile(SUFFIX => $suffix,
                                 UNLINK => 1);
    print $file $data;
    close $file;
    return $name;
}


{
    my $dl = downloader;
    for (@ARGV) {
        if (m,^https?://,) {
            my $data;
            next if $dl->($_, \$data) != 0;
            $_ = save_to_temp_file($data, $_);
        }
    }
}

system @ARGV;
